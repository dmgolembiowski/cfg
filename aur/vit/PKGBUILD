# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>

pkgname=vit
pkgver=1.3
pkgrel=1
pkgdesc="A terminal interface for Taskwarrior with Vim key bindings and colorization support"
arch=('any')
url="https://github.com/scottkosty/vit"
license=('GPL3')
depends=('perl-curses' 'perl-try-tiny' 'perl-text-charwidth' 'task')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('45ae4a1d08560d534a02a046d2fcf55c5401f9aaab0abcfbd13070fee28854268ba327018aea40317a184b9e28b074973f5d3e4169a479227e94b8401f066527')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
